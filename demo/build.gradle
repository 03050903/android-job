apply plugin: 'com.android.application'
apply from: '../build-config/gradle-quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.evernote.android.job.demo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles 'proguard.cfg'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            signingConfig signingConfigs.debug
        }

        /*
        gcm.initWith(buildTypes.debug)
        gcm {
            applicationIdSuffix ".gcm"
        }
        */
    }

    flavorDimensions "gcm"

    productFlavors {
        withGcm {
            dimension "gcm"
            applicationId "com.evernote.android.job.demo.gcm"
        }

        withoutGcm {
            dimension "gcm"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'release' && variant.flavors.get(0).name == "withoutGcm") {
            variant.setIgnore(true)
        }
    }
}

dependencies {
    implementation project(':library')
    implementation 'net.vrallev.android:cat:1.0.5'
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.facebook.stetho:stetho:$stethoVersion"

    withGcmImplementation "com.google.android.gms:play-services-gcm:$playServicesVersion"
}

uploadArchives.enabled false